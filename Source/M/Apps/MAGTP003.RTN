MAGTP003 ;WOIFO/FG,MLH,JSL - TELEPATHOLOGY RPCS ; 25 June 2013 10:19 PM
 ;;3.0;IMAGING;**138**;Mar 19, 2002;Build 4;Apr 03, 2013
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q  ;
 ;
 ;***** GET ALL THE LR (LAB DATA) REPORTS FOR A PATIENT, IDENTIFIED BY DFN
 ; RPC: MAGTP GET PATIENT
 ;
 ; .MAGRY        Reference to a local variable where the results
 ;               are returned to.
 ;
 ; DFN           Patient's ID number in the Patient file (#2)
 ; 
 ; STAT          1) If STAT is not null and in the Reading List
 ;                  in file (#2006.13) display the case if
 ;                  it has a consultation for an interpreting
 ;                  station number equal to STAT
 ;               2) If STAT is null, display all cases.
 ;
 ; Return Values
 ; =============
 ;
 ; If MAGRY(0) 1st '^'-piece is < 0, then an error
 ; occurred during execution of the procedure: [code]^^[error explanation]
 ;
 ; Otherwise, the output array is as follows:
 ;
 ; MAGRY(0)     Description
 ;                ^01: 1
 ;                ^02: Total Number of Lines
 ;                ^03: "Patient's Reports"
 ;
 ; MAGRY(i)     Description
 ;                ^01: Case Number
 ;                ^02: Lock Entry (0/1 for Unlocked/Locked) 
 ;                ^03: Initials of who locked the case in the LAB DATA file (#63) 
 ;                ^04: Patient's Name
 ;                ^05: Patient's ID Number
 ;                ^06: Priority
 ;                ^07: Slide(s) Available
 ;                ^08: Date/Time Specimen Taken
 ;                ^09: Case Status
 ;                ^10: Site Initials
 ;                ^11: AP Section
 ;                ^12: Year
 ;                ^13: Accession Number
 ;                ^14: ICN
 ;                ^15: Specimen Count
 ;                ^16: Reading Method
 ;                ^17: Patient's Short ID
 ;                ^18: Is there a Note? (Yes/No)
 ;                ^19: Employee/Sensitive? (1=Yes/0=No)
 ;                ^20: Number of image(s)
GETPAT(MAGRY,DFN,STAT) ; RPC [MAGTP GET PATIENT]
 N $ETRAP,$ESTACK S $ETRAP="D ERRA^MAGUTERR"
 K MAGRY
 I $G(DFN)="" S MAGRY(0)="-5^^No DFN Sent" Q
 I DFN'?1.N S MAGRY(0)="-4^^Invalid DFN Format" Q
 I '$D(^DPT(DFN)) S MAGRY(0)="-3^^Patient DFN Not Found "_DFN Q  ; ICR #350
 N LRDFN,PNM,CT,LRSS,LRSF,LRI,IEN,LRAA,LRAN,YEAR
 N RDATE,FLAG,LRAC,REC,OUTPUT,EMPSENS,SITE,MAGIEN,SELFLAG,TYPE,RSITE
 S LRDFN=$$GET1^DIQ(2,DFN_",",63)              ; Supported IA #10035
 I LRDFN="" S MAGRY(0)="-2^^No Lab Records For Patient" Q
 S PNM=$$GET1^DIQ(2,DFN_",",.01)               ; Supported IA #10035
 S SELFLAG=0
 ; If STAT is passed, check if it is in the Reading List file(#2006.13)
 I $G(STAT)'="" D  Q:$G(MAGRY(0))<0
 . S SITE=$$IEN^XUAF4(STAT)                    ; IA #2171 ; Site Number
 . I SITE="" S MAGRY(0)="-3^^Invalid Site "_STAT Q
 . I $O(^MAG(2006.13,"B",SITE,0)) S MAGRY(0)=1
 . S RSITE=0 F  S RSITE=$O(^MAG(2006.13,RSITE)) Q:'RSITE  D
 . . S MAGIEN=$O(^MAG(2006.13,RSITE,5,"B",SITE,"")) S:MAGIEN MAGRY(0)=1
 . . ; Check type of Reading Site:
 . . S TYPE=$$GET1^DIQ(2006.135,MAGIEN_","_RSITE_",",.02)
 . . I TYPE="CONSULTATION" S SELFLAG=1  ;select only the cases with their station number matching STAT
 . . Q
 . I $G(MAGRY(0))'=1 S MAGRY(0)="-2^^Site Not in Reading List" Q
 . Q
 ;
 S CT=0
 F LRSS="CY","EM","SP" D                       ; Only three AP sections considered
 . S LRSF=$S(LRSS="CY":63.09,LRSS="EM":63.02,1:63.08)
 . S LRI=0  ;old cases
 . F  S LRI=$O(^LR(LRDFN,LRSS,LRI)) Q:LRI=""  D
 . . S IEN=LRI_","_LRDFN_","
 . . S RDATE=+$$GET1^DIQ(LRSF,IEN,.11,"I")     ; Release date if any
 . . S FLAG=$S(RDATE:1,1:0)
 . . S LRAC=$$GET1^DIQ(LRSF,IEN,.06)           ; Case Number
 . . Q:LRAC=""  Q:$G(LRAC(LRAC))               ; Skip null entries/duplicate
 . . I SELFLAG Q:'$$ISCONSLT^MAGTP009(LRAC,SITE)   ; Quit if no consultations for that case and site
 . . S REC=$O(^MAG(2005.42,"B",LRAC,""))_","   ; Record Number
 . . ; OUTPUT contains pieces ^02:^19 defined above in the MAGRY(i) description
 . . S OUTPUT=$$GETCASE^MAGTP009(LRSS,LRAC,LRSF,IEN,REC,FLAG,PNM,DFN)
 . . S CT=CT+1,MAGRY(CT)=LRAC_U_OUTPUT,EMPSENS=$P(MAGRY(CT),"^",19),LRAC(LRAC)=CT
 . . Q
 . S YEAR=" ",LRAA=$O(^LRO(68,"B",LRSS,0)) ;new style cases
 . F  S YEAR=$O(^LRO(68,LRAA,1,YEAR),-1) Q:'YEAR  D  ; Most recent first
 . . S LRAN=" "
 . . F  S LRAN=$O(^LRO(68,LRAA,1,YEAR,1,LRAN),-1) Q:'LRAN  D  ; Most recent first
 . . . I LRDFN'=+$P($G(^LRO(68,LRAA,1,YEAR,1,LRAN,0)),"^",1) Q
 . . . S LRAC=$G(^LRO(68,LRAA,1,YEAR,1,LRAN,.2)),SITE=+$G(^(.4),DUZ(2))
 . . . Q:LRAC=""  Q:'$D(^MAG(2006.13,"B",SITE))  Q:$G(LRAC(LRAC))
 . . . I SELFLAG Q:'$$ISCONSLT^MAGTP009(LRAC,SITE)   ; Quit if no consultations for that case and site
 . . . S LRI=$P(^LRO(68,LRAA,1,YEAR,1,LRAN,3),"^",5) Q:'LRI
 . . . S IEN=LRI_","_LRDFN_","
 . . . S REC=$O(^MAG(2005.42,"B",LRAC,""))_","   ; Record Number
 . . . S PNM=$$GET1^DIQ(63,LRDFN_",",".03")
 . . . S DFN=$$GET1^DIQ(63,LRDFN_",",".03","I"),FLAG=$G(FLAG,0)
 . . . S OUTPUT=$$GETCASE^MAGTP009(LRSS,LRAC,LRSF,IEN,REC,FLAG,PNM,DFN)
 . . . S CT=CT+1,MAGRY(CT)=LRAC_U_OUTPUT,EMPSENS=$P(MAGRY(CT),"^",19),LRAC(LRAC)=CT
 . . . Q
 . . Q
 . Q
 ;
 ; Header
 ;
 S $P(MAGRY(0),"^",1)=$S('$G(EMPSENS):0,1:1)
 S $P(MAGRY(0),"^",2)=CT
 S $P(MAGRY(0),"^",3)="Patient's Reports"_$S('$G(EMPSENS):"",1:" (Employee/Sensitive)")
 Q  ;
 ;
 ;***** SET/UNSET A LOGICAL LOCK ON A RECORD FOR CASE RESERVATION
 ; RPC: MAGTP RESERVE CASE
 ;
 ; .MAGRY        Reference to a local variable where the results
 ;               are returned to.
 ;
 ; LFLAG         Flag that controls whether to lock or unlock
 ;               (0:Unlocked, 1:Locked)
 ;
 ; LRSS          AP Section
 ;
 ; YEAR          Accession Year (Two figures)
 ;
 ; LRAN          Accession Number
 ;
 ; Return Values
 ; =============
 ;
 ; If MAGRY(0) 1st '^'-piece is < 0, then an error
 ; occurred during execution of the procedure: <code>^0^ ERROR explanation
 ;
 ; Otherwise, the output array is as follows:
 ;
 ; MAGRY(0)     Description
 ;                ^01: 0
 ;                ^02: 0 if case record is unlocked, 1 if case record is locked
 ;                ^03: "Reservation ended" if case record is unlocked
 ;                     "Case reserved" if case record is locked
 ;
LOCKR(MAGRY,LFLAG,LRSS,YEAR,LRAN) ; RPC [MAGTP RESERVE CASE]
 K MAGRY
 N $ETRAP,$ESTACK S $ETRAP="D ERRA^MAGUTERR"
 D  Q:$G(MAGRY(0))  ; validate inputs
 . I $G(LRSS)="" S MAGRY(0)="-2^^ERROR: AP subsection not specified" Q
 . I $G(YEAR)="" S MAGRY(0)="-3^^ERROR: Year not specified" Q
 . I $G(LRAN)="" S MAGRY(0)="-4^^ERROR: Accession index not specified" Q
 . Q
 N INPUT
 D  Q:$G(MAGRY(0))  ; validate context
 . N OUT
 . S INPUT=$$CONTEXT^MAGTP006(.OUT,LRSS,YEAR,LRAN)
 . D:'$G(OUT(0))  ; context not OK
 . . S MAGRY(0)="-5^^ERROR: Invalid context - "
 . . S MAGRY(0)=MAGRY(0)_$P(OUT(0),"^",3)
 . . Q
 . Q
 N MAGFDA,MAGERR
 N LRSF,IEN,LRAC,REC,ISLOCK
 N LDT,LDUZ,LUSER,TEXT,LRAA,YR
 S LRSF=$P(INPUT,","),IEN=$P(INPUT,",",2,4)
 S LRAC=$$GET1^DIQ(LRSF,IEN,.06)               ; Accession code
 D:LRAC=""   ;try new style (LEDI)
 . S LRAA=$O(^LRO(68,"B",LRSS,0)) ;new style cases
 . S YR=$S($L(YEAR)=2:300+YEAR*10000,1:YEAR) ;try 2000 
 . I +$P($G(^LRO(68,LRAA,1,YR,1,LRAN,0)),"^",1) S LRAC=$G(^LRO(68,LRAA,1,YR,1,LRAN,.2)) Q:LRAC]""
 . S YR=$S($L(YEAR)=2:200+YEAR*10000,1:YEAR) ;try 1900
 . I +$P($G(^LRO(68,LRAA,1,YR,1,LRAN,0)),"^",1) S LRAC=$G(^LRO(68,LRAA,1,YR,1,LRAN,.2))
 . Q
 I LRAC="" S MAGRY(0)="-6^^ERROR: No Accession Code for this case " Q
 S REC=$O(^MAG(2005.42,"B",LRAC,""))_","       ; Record number
 ;
 ; Update lock record with present user's data (or clear lock)
 ;
 S MAGFDA(2005.42,REC,1)=LFLAG
 S MAGFDA(2005.42,REC,1.1)=$S(LFLAG:$$NOW^XLFDT,1:"")
 S MAGFDA(2005.42,REC,1.2)=$S(LFLAG:DUZ,1:"")
 D UPDATE^DIE("","MAGFDA","","MAGERR")         ; Update lock record
 I $D(MAGERR) S MAGRY(0)="-7^^ERROR: Update error - "_MAGERR("DIERR",1,"TEXT",1) Q
 ;
 S TEXT=$S(LFLAG:"1^Case reserved",1:"0^Reservation ended")
 S MAGRY(0)="0^"_TEXT
 Q  ;
 ;
 ;***** GET THE NUMBER OF MINUTES FROM FILE (#2006.13)
 ;      A LOGICAL LOCK IS EFFECTIVE UNTIL IT EXPIRES
 ; RPC: MAGTP GET LOCK MINUTES
 ;
 ; .MAGRY        Reference to a local variable where the results
 ;               are returned to.
 ;
 ; Input Parameters
 ; ================
 ; N/A
 ;
 ; Return Values
 ; =============
 ;
 ; If MAGRY(0) 1st '^'-piece is 0, then an error
 ; occurred during execution of the procedure: 0^0^ ERROR explanation
 ;
 ; Otherwise, the output array is as follows:
 ;
 ; MAGRY(0)     Description
 ;                ^01: 1
 ;                ^02: 1
 ;
 ; MAGRY(1)     Description
 ;                ^01: Number of Minutes
 ;
GETLMINS(MAGRY) ; RPC [MAGTP GET LOCK MINS]
 K MAGRY
 N $ETRAP,$ESTACK S $ETRAP="D ERRA^MAGGTERR"
 N MINS
 S MINS=+$$GET1^DIQ(2006.13,"1,",4)           ; How many minutes before ignoring lock
 S MAGRY(0)="1^1"
 S MAGRY(1)=MINS
 Q  ;
 ;
 ;***** SET THE NUMBER OF MINUTES IN FILE (#2006.13)
 ;      A LOGICAL LOCK IS EFFECTIVE UNTIL IT EXPIRES
 ; RPC: MAGTP PUT LOCK MINUTES
 ;
 ; .MAGRY        Reference to a local variable where the results
 ;               are returned to.
 ;
 ; MINUTES       Number of Minutes
 ;
 ; Return Values
 ; =============
 ;
 ; If MAGRY(0) 1st '^'-piece is 0, then an error
 ; occurred during execution of the procedure: 0^0^ ERROR explanation
 ;
 ; Otherwise, the output array is as follows:
 ;
 ; MAGRY(0)     Description
 ;                ^01: 1
 ;                ^02: 0
 ;                ^03: "Number of Locking Minutes Updated"
 ;
PUTLMINS(MAGRY,MINS) ; RPC [MAGTP PUT LOCK MINS]
 K MAGRY
 N $ETRAP,$ESTACK S $ETRAP="D ERRA^MAGGTERR"
 I +$G(MINS)<0!(+$G(MINS)>9999) D  Q
 . S MAGRY(0)="0^0^Invalid number of minutes"
 N MAGFDA,MAGERR
 S MAGFDA(2006.13,"1,",4)=MINS
 D UPDATE^DIE("E","MAGFDA","","MAGERR")
 I $D(MAGERR) D  Q
 . S MAGRY(0)="0^0^Updating Error: "_MAGERR("DIERR",1,"TEXT",1)
 S MAGRY(0)="1^0^Number of Locking Minutes Updated"
 Q  ;
