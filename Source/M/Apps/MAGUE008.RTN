MAGUE008 ;WOIFO/MLH - find an accession identifier for a LAB DATA entry ; 11 Apr 2013 1:05 PM
 ;;3.0;IMAGING;**138**;Mar 19, 2002;Build 4;Jan 15, 2013
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q  ;
 ;
 ; This routine eliminates dependency on the accession number having been
 ; stored on the LAB DATA file (#63).  If necessary, it crawls the ACCESSION
 ; file (#68) in reverse chronological order.
 ; 
 ;***** FIND AN ACCESSION IDENTIFIER GIVEN LAB DATA SUBSCRIPT INFORMATION
 ;
 ; LRSS          AP Section
 ; 
 ; LRDFN         Internal entry number of patient record in LAB DATA file (#63)
 ;
 ; LRI           Inverse-date subscript of order record in LAB DATA file (#63)
 ;
 ; Return Values
 ; =============
 ;
 ; .ACCID  Accession identifier if one exists for this order
 ; 
FIND(ACCID,LRSS,LRDFN,LRI) ;
 N LRSSIX,YR,ACCIX
 Q:$G(LRSS)=""  Q:$G(LRDFN)=""  Q:$G(LRI)=""
 ; is accession recorded on LAB DATA file (#63)?
 S ACCID=$P($G(^LR(LRDFN,LRSS,LRI,0)),"^",6)
 D:ACCID=""  ; no, must crawl the ACCESSION file (#68)
 . S LRSSIX="" F  S LRSSIX=$O(^LRO(68,"B",LRSS,LRSSIX)) Q:LRSSIX=""  Q:$G(^LRO(68,"B",LRSS,LRSSIX))
 . D:$G(LRSSIX)'=""
 . . S YR=" " F  S YR=$O(^LRO(68,LRSSIX,1,YR),-1) Q:'YR  D  Q:ACCID'=""
 . . . S ACCIX=" " F  S ACCIX=$O(^LRO(68,LRSSIX,1,YR,1,ACCIX),-1) Q:'ACCIX  D  Q:ACCID'=""
 . . . . I $P($G(^LRO(68,LRSSIX,1,YR,1,ACCIX,0)),"^",1)=LRDFN,$P($G(^(3)),"^",5)=LRI S ACCID=$P($G(^(.2)),"^",1)
 . . . . Q
 . . . Q
 . . Q
 . Q
 Q
