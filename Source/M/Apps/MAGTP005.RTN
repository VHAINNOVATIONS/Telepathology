MAGTP005 ;WOIFO/FG/PMK - TELEPATHOLOGY RPCS ; 28 Jun 2013 7:48 PM
 ;;3.0;IMAGING;**138**;Mar 19, 2002;Build 4;Apr 03, 2013
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;; +---------------------------------------------------------------+
 ;; | Property of the US Government.                                |
 ;; | No permission to copy or redistribute this software is given. |
 ;; | Use of unreleased versions of this software requires the user |
 ;; | to execute a written test agreement with the VistA Imaging    |
 ;; | Development Office of the Department of Veterans Affairs,     |
 ;; | telephone (301) 734-0100.                                     |
 ;; | The Food and Drug Administration classifies this software as  |
 ;; | a medical device.  As such, it may not be changed in any way. |
 ;; | Modifications to this software may result in an adulterated   |
 ;; | medical device under 21CFR820, the use of which is considered |
 ;; | to be a violation of US Federal Statutes.                     |
 ;; +---------------------------------------------------------------+
 ;;
 Q  ;
 ;
 ;***** GET THE XML TEMPLATE FOR THE PRESENT SITE, ACCORDING TO THE AP SECTION
 ; RPC: MAGTP GET TEMPLATE XML
 ;
 ; .MAGRY        Reference to a local variable where the results
 ;               are returned to.
 ;
 ; LRSS          AP Section
 ;
 ; Return Values
 ; =============
 ;
 ; If MAGRY(0) 1st '^'-piece is 0, then an error
 ; occurred during execution of the procedure: 0^0^ ERROR explanation
 ;
 ; Otherwise, the output array is as follows:
 ;
 ; MAGRY(0)     Description
 ;                ^01: 1
 ;                ^02: Total Number of Lines
 ;
 ; MAGRY(i)     Description
 ;                ^01: XML Line of Text
 ;
GETXT(MAGRY,LRSS) ; RPC [MAGTP GET TEMPLATE XML]
 K MAGRY
 N $ETRAP,$ESTACK S $ETRAP="D ERRA^MAGGTERR"
 N MAGOUT,MAGERR
 N NUM,CT,I
 S NUM=$S(LRSS="CY":1,LRSS="EM":2,LRSS="SP":3,1:"")
 I NUM="" S MAGRY(0)="0^0^Invalid AP Section" Q
 D GET1^DIQ(2006.13,"1,",NUM,"","MAGOUT","MAGERR")
 I $D(MAGERR) D  Q
 . S MAGRY(0)="0^0^Access Error: "_MAGERR("DIERR",1,"TEXT",1)
 S (CT,I)=0
 F  S I=$O(MAGOUT(I)) Q:I=""  D
 . S CT=CT+1
 . S MAGRY(CT)=MAGOUT(I)
 . Q
 S MAGRY(0)="1^"_CT
 Q  ;
 ;
 ;***** SET THE XML TEMPLATE FOR THE PRESENT SITE, ACCORDING TO THE AP SECTION
 ; RPC: MAGTP PUT TEMPLATE XML
 ;
 ; .MAGRY        Reference to a local variable where the results
 ;               are returned to.
 ;
 ; .ENT          Input array. One XML line of text must be
 ;               on each line of the array
 ;
 ; LRSS          AP Section
 ;
 ; Return Values
 ; =============
 ;
 ; If MAGRY(0) 1st '^'-piece is 0, then an error
 ; occurred during execution of the procedure: 0^0^ ERROR explanation
 ;
 ; Otherwise, the output array is as follows:
 ;
 ; MAGRY(0)     Description
 ;                ^01: 1
 ;                ^02: 0
 ;                ^03: "<LRSS> Template Updated"
 ;
PUTXT(MAGRY,ENT,LRSS) ; RPC [MAGTP PUT TEMPLATE XML]
 K MAGRY
 N $ETRAP,$ESTACK S $ETRAP="D ERRA^MAGGTERR"
 I $D(ENT)<10 S MAGRY(0)="0^0^No Input" Q      ; Input: Text on each line
 N MAGFDA,MAGERR
 N NUM,I,LINE
 S NUM=$S(LRSS="CY":1,LRSS="EM":2,LRSS="SP":3,1:"")
 I NUM="" S MAGRY(0)="0^0^Invalid AP Section" Q
 S (I,LINE)=""
 F  S LINE=$O(ENT(LINE)) Q:LINE=""  D
 . S I=I+1
 . S MAGFDA(I)=ENT(LINE)                       ; FDA arrays cannot start from 0
 . Q
 D WP^DIE(2006.13,"1,",NUM,"K","MAGFDA","MAGERR") ; Whole text killed for WP
 I $D(MAGERR) D  Q
 . S MAGRY(0)="0^0^Updating Error: "_MAGERR("DIERR",1,"TEXT",1)
 S MAGRY(0)="1^0^"_LRSS_" Template Updated"
 Q  ;
 ;
 ;***** SET PRIORITY FOR A CASE BY UPDATING FILE (#2005.42)
 ; RPC: MAGTP PUT PRIORITY
 ;
 ; .MAGRY        Reference to a local variable where the results
 ;               are returned to.
 ;
 ; PRI           Priority (0:ROUTINE, 1:HIGH, 2:STAT). Default ROUTINE.
 ;
 ; LRSS          AP Section
 ;
 ; YEAR          Accession Year (Two figures)
 ;
 ; LRAN          Accession Number
 ;
 ; Return Values
 ; =============
 ;
 ; If MAGRY(0) 1st '^'-piece is 0, then an error
 ; occurred during execution of the procedure:
 ;
 ; Otherwise, the output array is as follows:
 ;
 ; MAGRY(0)     Description
 ;                ^01: 1
 ;                ^02: 0
 ;                ^03: "Priority Updated"
 ;
PUTPRI(MAGRY,PRI,LRSS,YEAR,LRAN) ; RPC [MAGTP PUT PRIORITY]
 K MAGRY
 N $ETRAP,$ESTACK S $ETRAP="D ERRA^MAGGTERR"
 N INPUT
 S INPUT=$$CONTEXT^MAGTP006(.MAGRY,LRSS,YEAR,LRAN) Q:'MAGRY(0)
 N MAGFDA,MAGERR
 N LRSF,IEN,LRAC,REC
 S LRSF=$P(INPUT,","),IEN=$P(INPUT,",",2,4)
 S LRAC=$$GET1^DIQ(LRSF,IEN,.06)               ; Accession Number
 I LRAC="" S MAGRY(0)="0^0^No Case Number" Q
 S REC=$O(^MAG(2005.42,"B",LRAC,""))_","       ; Record Number
 I REC="," S MAGRY(0)="0^0^Record Not Found" Q
 S:$G(PRI)="" PRI=0                            ; Default to ROUTINE
 S MAGFDA(2005.42,REC,.02)=PRI
 D UPDATE^DIE("","MAGFDA","","MAGERR")
 I $D(MAGERR) S MAGRY(0)="0^0^Updating Error: "_MAGERR("DIERR",1,"TEXT",1) Q
 S MAGRY(0)="1^0^Priority Updated"
 Q  ;
 ;
 ; +++++ ADD A NEW ENTRY IN FILE (#2005.42) WHEN
 ;       GENERATING A NEW CASE FROM FILEMAN
 ;
 ; LRAC          Accession Code
 ;
ADD(LRAC) ;
 Q:'$$ISLRSSOK^MAGT7MA(LRSS)  ; check for supported anatomic pathology sections
 ;
 N MAGFDA,MAGERR,NOW
 S NOW=$$NOW^XLFDT
 S MAGFDA(2005.42,"+1,",.01)=LRAC              ; Accession Number
 S MAGFDA(2005.42,"+1,",.02)=0                 ; Priority (0:ROUTINE, 1:HIGH, 2:STAT)
 S MAGFDA(2005.42,"+1,",.03)=0                 ; Slide available? (0:No, 1:Yes)
 S MAGFDA(2005.42,"+1,",.04)=0                 ; Method (0:TRADITIONAL, 1:ROBOTICS, 2:WSI)
 S MAGFDA(2005.42,"+1,",.05)="U"               ; Status (U:Unread, R:Read, C:Cancelled)
 S MAGFDA(2005.42,"+1,",.06)=NOW               ; Start
 S MAGFDA(2005.42,"+1,",.07)=NOW               ; Last Activity
 S MAGFDA(2005.42,"+1,",.08)=0                 ; Number of Images
 S MAGFDA(2005.42,"+1,",1)=0                   ; Reservation (0:Not Reserved, 1:Reserved)
 S MAGFDA(2005.42,"+1,",2)=0                   ; Second  Lock (Accession) (0:Unlocked, 1:Locked)
 D UPDATE^DIE("","MAGFDA","","MAGERR")
 Q
 ;
STATUPDT(LRAC,STATUS) ; update the state of the case
 N IENS,MAGFDA,MAGERR,NOW
 ;
 Q:$G(LRAC)=""  Q:$G(STATUS)=""
 S STATUS=$E(STATUS) Q:"URC"'[STATUS
 ;
 S IENS=$O(^MAG(2005.42,"B",LRAC,"")) Q:'IENS
 ;
 ; update the entry
 S IENS=IENS_","
 S NOW=$$NOW^XLFDT
 S MAGFDA(2005.42,IENS,.05)=STATUS            ; Status (U:Unread, R:Read, C:Cancelled)
 S MAGFDA(2005.42,IENS,.07)=NOW               ; Last Activity
 D UPDATE^DIE("","MAGFDA","","MAGERR")
 Q
 ;
CANCEL(LRAC) ; case cancelled - remove it from the file
 N DA,DIK,DA
 ;
 Q:$G(LRAC)=""
 S DA=$O(^MAG(2005.42,"B",LRAC,"")) Q:'DA
 ;
 ; delete the entry
 S DIK="^MAG(2005.42,"
 D ^DIK
 Q
 ;
IMAGECNT(LRAC,COUNT) ; update the number of images of the case
 N IENS,MAGFDA,MAGERR,NOW,NIMAGES,TOTAL
 ;
 Q:$G(LRAC)=""
 S COUNT=$G(COUNT,1)
 ;
 S IENS=$O(^MAG(2005.42,"B",LRAC,"")) Q:'IENS
 S IENS=IENS_","
 S NIMAGES=$$GET1^DIQ(2005.42,IENS,.08)
 S TOTAL=NIMAGES+COUNT
 ;
 ; update the entry
 S NOW=$$NOW^XLFDT
 S MAGFDA(2005.42,IENS,.07)=NOW               ; Last Activity
 S MAGFDA(2005.42,IENS,.08)=TOTAL             ; Number of Images
 D UPDATE^DIE("","MAGFDA","","MAGERR")
 Q
